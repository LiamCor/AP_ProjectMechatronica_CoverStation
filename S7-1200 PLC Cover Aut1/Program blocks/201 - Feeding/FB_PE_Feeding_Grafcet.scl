FUNCTION_BLOCK "FB_PE_Feeding_Grafcet"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      i_Init : Bool;
      i_Enable : Bool;
      iSenCilOut : Bool;
      iSenLoaded : Bool;
   END_VAR

   VAR_OUTPUT 
      o_Ready : Bool;
      o_AlmLowMaterial : Bool;
      oCilOut : Bool;
   END_VAR

   VAR 
      sStep : Int;
      sInit : Bool;
      sTON_X0 {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
      sTON_X1 {InstructionName := 'TON_TIME'; LibVersion := '1.0'} : TON_TIME;
   END_VAR


BEGIN
	REGION init
	    IF #i_Init AND NOT (#sInit = #i_Init) THEN
	        #sStep := 0;
	    END_IF;
	    #sInit := #i_Init;
	END_REGION
	
	REGION Enable
	    IF NOT #i_Enable THEN
	        GOTO afterGrafcetLogic;
	    END_IF;
	END_REGION
	
	REGION Timers
	    
	    #sTON_X0(IN := NOT #iSenLoaded AND #sStep = 0,
	             PT := T#0.5S);
	    
	    #sTON_X1(IN := #iSenCilOut AND NOT #iSenLoaded AND #sStep = 1,
	                                PT := T#0.5S);
	    
	END_REGION
	
	REGION Grafcet logic
	    CASE #sStep OF
	        0: // o_Ready
	            IF #sTON_X0.Q THEN
	                #sStep := 1;
	            END_IF;
	        1: // oCilOut
	            IF #iSenCilOut AND #iSenLoaded THEN
	                #sStep := 0;
	            ELSIF #sTON_X1.Q THEN
	                #sStep := 2;
	            END_IF;
	        2: // o_AlmLowMaterial
	            ;
	        ELSE
	            #sStep := 0;
	    END_CASE;
	END_REGION
	
	afterGrafcetLogic: // Jump destination
	
	REGION Outputs
	    
	    #oCilOut := #sStep = 1;
	    #o_Ready := #sStep = 0;
	    #o_AlmLowMaterial := #sStep = 2;
	    
	END_REGION
END_FUNCTION_BLOCK

